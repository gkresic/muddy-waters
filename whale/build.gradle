apply plugin: 'application'

dependencies {

	implementation 'ch.qos.logback:logback-core:1.5.6'
	implementation 'ch.qos.logback:logback-classic:1.5.6'
	implementation 'org.slf4j:slf4j-api:2.0.13'

	implementation 'com.google.inject:guice:7.0.0'
	implementation 'com.google.inject.extensions:guice-servlet:7.0.0'

	implementation 'org.eclipse.jetty:jetty-core:12.0.11'
	implementation 'org.eclipse.jetty:jetty-server:12.0.11'
	implementation 'org.eclipse.jetty.ee10:jetty-ee10-webapp:12.0.11'

	implementation 'org.jboss.resteasy:resteasy-jaxrs-all:6.2.9.Final'
	implementation 'org.jboss.resteasy:resteasy-servlet-initializer:6.2.9.Final'
	implementation 'org.jboss.resteasy:resteasy-jackson2-provider:6.2.9.Final'
	implementation 'dev.resteasy.guice:resteasy-guice:1.0.0.Alpha1'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
}

distributions {
	main {
		contents {
			rename('jetty.template.xml', 'jetty.xml')
			rename('logback.template.xml', 'logback.xml')
		}
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$WHALE_CONF:$APP_HOME/conf:') 
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%WHALE_CONF%;%APP_HOME%\\conf;')
	}
}

application {
	mainClass = 'com.steatoda.muddywaters.whale.WhaleCmd'
}

run {
	classpath += files("${System.env.WHALE_CONF}", "${System.properties.WHALE_CONF}", "${project.buildDir}/install/whale/conf")
	standardInput = System.in
}

build.dependsOn installDist

distTar.enabled = false
distZip.enabled = false
