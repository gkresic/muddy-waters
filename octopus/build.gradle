apply plugin: 'application'

ext {
	logbackVersion = '1.5.6'
	jettyVersion = '12.0.11'
}

dependencies {

	implementation "ch.qos.logback:logback-core:${logbackVersion}"
	implementation "ch.qos.logback:logback-classic:${logbackVersion}"
	implementation 'org.slf4j:slf4j-api:2.0.13'

	implementation "org.eclipse.jetty:jetty-core:${jettyVersion}"
	implementation "org.eclipse.jetty:jetty-xml:${jettyVersion}"
	implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
	implementation "org.eclipse.jetty.ee10:jetty-ee10-servlet:${jettyVersion}"

	implementation 'com.graphql-java-kickstart:graphql-java-servlet:15.2.0'

}

application {
	mainClass = 'com.steatoda.muddywaters.octopus.OctopusCmd'
}

distributions {
	main {
		contents {
			rename('jetty.template.xml', 'jetty.xml')
			rename('logback.template.xml', 'logback.xml')
		}
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$OCTOPUS_CONF:$APP_HOME/conf:')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%OCTOPUS_CONF%;%APP_HOME%\\conf;')
	}
}

run {
	classpath += files("${System.env.OCTOPUS_CONF}", "${System.properties.OCTOPUS_CONF}", "${project.buildDir}/install/octopus/conf")
	standardInput = System.in
}

build.dependsOn installDist

distTar.enabled = false
distZip.enabled = false
