apply plugin: 'application'

dependencies {

	implementation 'ch.qos.logback:logback-classic:1.5.18'
	implementation 'org.slf4j:slf4j-api:2.0.17'
	implementation 'org.slf4j:jul-to-slf4j:2.0.17'

	implementation 'org.greenrobot:eventbus-java:3.3.1'
	annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.3.1'

	implementation 'com.google.dagger:dagger:2.44'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.44'

	implementation 'io.helidon.common:helidon-common-media-type:4.2.2'
	implementation 'io.helidon.config:helidon-config-yaml:4.2.2'
	implementation 'io.helidon.http.media:helidon-http-media-jackson:4.2.2'
	implementation 'io.helidon.webserver:helidon-webserver:4.2.2'
	implementation 'io.helidon.logging:helidon-logging-slf4j:4.2.2'

	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.0'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.19.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'

	implementation 'com.dslplatform:dsl-json:2.0.2'
	annotationProcessor 'com.dslplatform:dsl-json:2.0.2'

}

processResources {
	filesMatching(["**/kaluga.properties"]) {
		expand version: version
	}
}

compileJava {
	// https://greenrobot.org/eventbus/documentation/subscriber-index/
	options.compilerArgs += '-AeventBusIndex=com.steatoda.muddywaters.kaluga.KalugaEventBusIndex'
}

application {
	mainClass = 'com.steatoda.muddywaters.kaluga.KalugaCmd'
}

distributions {
	main {
		contents {
			rename('helidon.template.yaml', 'helidon.yaml')
			rename('logback.template.xml', 'logback.xml')
		}
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$KALUGA_CONF:$APP_HOME/conf:')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%KALUGA_CONF%;%APP_HOME%\\conf;')
	}
}

run {
	classpath += files("${System.env.KALUGA_CONF}", "${System.properties.KALUGA_CONF}", "${project.buildDir}/install/kaluga/conf")
	standardInput = System.in
}

build.dependsOn installDist

distTar.enabled = false
distZip.enabled = false
