apply plugin: 'application'

dependencies {

	implementation 'ch.qos.logback:logback-core:1.4.3'
	implementation 'ch.qos.logback:logback-classic:1.4.3'
	implementation 'org.slf4j:slf4j-api:2.0.3'

	implementation 'org.greenrobot:eventbus-java:3.3.1'
	annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.3.1'

	implementation 'com.google.dagger:dagger:2.44'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.44'

	implementation 'io.vertx:vertx-web:4.3.4'

	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.13.4'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'

	implementation 'com.dslplatform:dsl-json:1.9.9'
	implementation 'com.dslplatform:dsl-json-java8:1.9.9'
	annotationProcessor 'com.dslplatform:dsl-json-java8:1.9.9'

}

processResources {
	filesMatching(["**/dolphin.properties"]) {
		expand version: version
	}
}

compileJava {
	// https://greenrobot.org/eventbus/documentation/subscriber-index/
	options.compilerArgs += '-AeventBusIndex=com.steatoda.muddywaters.dolphin.DolphinEventBusIndex'
}

application {
	mainClass = 'com.steatoda.muddywaters.dolphin.DolphinCmd'
}

distributions {
	main {
		contents {
			rename('logback.template.xml', 'logback.xml')
			rename('vertx.template.json', 'vertx.json')
			rename('vertx-http-server.template.json', 'vertx-http-server.json')
		}
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$DOLPHIN_CONF:$APP_HOME/conf:')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%DOLPHIN_CONF%;%APP_HOME%\\conf;')
	}
}

run {
	classpath += files("${System.env.DOLPHIN_CONF}", "${System.properties.DOLPHIN_CONF}", "${project.buildDir}/install/dolphin/conf")
	standardInput = System.in
}

build.dependsOn installDist

distTar.enabled = false
distZip.enabled = false
