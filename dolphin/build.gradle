plugins {
	id 'org.beryx.runtime' version '1.13.0'
}

apply plugin: 'application'

dependencies {

	implementation 'ch.qos.logback:logback-core:1.5.18'
	implementation 'ch.qos.logback:logback-classic:1.5.18'
	implementation 'org.slf4j:slf4j-api:2.0.17'

	implementation 'org.greenrobot:eventbus-java:3.3.1'
	annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.3.1'

	implementation 'com.google.dagger:dagger:2.56.2'
	annotationProcessor 'com.google.dagger:dagger-compiler:2.56.2'

	implementation 'io.vertx:vertx-web:5.0.0'

	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.0'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.19.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'

	implementation 'com.dslplatform:dsl-json:2.0.2'
	annotationProcessor 'com.dslplatform:dsl-json:2.0.2'

}

processResources {
	filesMatching(["**/dolphin.properties"]) {
		expand version: version
	}
}

compileJava {
	// https://greenrobot.org/eventbus/documentation/subscriber-index/
	options.compilerArgs += '-AeventBusIndex=com.steatoda.muddywaters.dolphin.DolphinEventBusIndex'
}

runtime {
	options = [
		'--strip-debug',
		'--compress', '2',
		'--no-header-files',
		'--no-man-pages'
	]
	modules = [
		'java.xml'
	]
}

application {
	mainClass = 'com.steatoda.muddywaters.dolphin.DolphinCmd'
}

distributions {
	main {
		contents {
			rename('logback.template.xml', 'logback.xml')
			rename('vertx.template.json', 'vertx.json')
			rename('vertx-http-server.template.json', 'vertx-http-server.json')
		}
	}
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('CLASSPATH=', 'CLASSPATH=$DOLPHIN_CONF:$APP_HOME/conf:')
		windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%DOLPHIN_CONF%;%APP_HOME%\\conf;')
	}
}

run {
	classpath += files("${System.env.DOLPHIN_CONF}", "${System.properties.DOLPHIN_CONF}", "${project.buildDir}/install/dolphin/conf")
	standardInput = System.in
}

build.dependsOn installDist

distTar.enabled = false
distZip.enabled = false
